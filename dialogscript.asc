// CustomDialogGui 
// global vars
CustomDialogGui CDG;
int CDG_active_options[];
int CDG_active_options_height[];
int CDG_active_options_width[];
String CDG_active_options_text[];
bool in_speech;




/***********************************************************************
 * STATIC FUNCTIONS
 * GetDefaultParams_
 * Set and modify your native GUI options here
 *
 ***********************************************************************/
static int[] CustomDialogGui::GetDefaultSettings_Sprites()
{
  int sprites[] = new int[e9verb_sprites_enumsize];
  
  // Image Number for the Up-Arrow image
  // Highlight images are optional and only work properly in AGS >=3.4
  sprites[e9verb_uparrow_img]      = 124;
  sprites[e9verb_uparrow_hi_img]   = 137; 
  sprites[e9verb_uparrow_push_img]   = 0; //not used 

  //Image Number for the Down-Arrow image
  sprites[e9verb_downarrow_img]      = 128;
  sprites[e9verb_downarrow_hi_img]   = 141; 
  sprites[e9verb_downarrow_push_img]   = 0; //not used
  
  return sprites;
}

static int[] CustomDialogGui::GetDefaultSettings_Text()
{
  int textSettings[] = new int[e9verb_text_enumsize];
  
  textSettings[e9verb_text_font]        = eFontText;
  textSettings[e9verb_text_alignment]   = eAlignLeft;
  textSettings[e9verb_text_color]       = 15219;
  textSettings[e9verb_text_color_active]     = 38555;
  
  // optional background Image for the Text
  textSettings[e9verb_text_bg]           = 0;
  textSettings[e9verb_text_bg_xpos]      = 0;
  textSettings[e9verb_text_bg_scaling]   = 0;
  textSettings[e9verb_text_bg_transparency] = 0;
  
  textSettings[e9verb_text_line_space]   = 2;
  
  return textSettings;
}

static int[] CustomDialogGui::GetDefaultSettings_Borders()
{
  int bordersSettings[] = new int[e9verb_borders_enumsize];
  
  bordersSettings[e9verb_border_top]      = 5;
  bordersSettings[e9verb_border_bottom]   = 5;
  bordersSettings[e9verb_border_left]     = 13;
  bordersSettings[e9verb_border_right]    = 17;
  bordersSettings[e9verb_border_color]    = 4;
  
  return bordersSettings;
}

static int[] CustomDialogGui::GetDefaultSettings_Layout()
{
  int layoutSettings[] = new int[e9verb_layout_enumsize];
  
  // Top-Left corner of the Dialog GUI
  layoutSettings[e9verb_gui_xpos]        = 0;
  layoutSettings[e9verb_gui_ypos]        = 144;
  
  //Size of the whole Dialog GUI
  layoutSettings[e9verb_gui_width]       = 320;
  layoutSettings[e9verb_gui_height]      = 56;
  
  //Arrows XY coordinates in the gui
  //up arrow
  layoutSettings[e9verb_uparrow_xpos]     = 303; 
  layoutSettings[e9verb_uparrow_ypos]    = 1;
  //down arrow
  layoutSettings[e9verb_downarrow_xpos]     = 303;
  layoutSettings[e9verb_downarrow_ypos]     = 43;
  
  return layoutSettings;
}
static int[] CustomDialogGui::GetDefaultSettings_Autosize()
{
  int autoSizeSettings[] = new int[e9verb_autosize_enumsize];
  
  autoSizeSettings[e9verb_autosize_minheight]    = 20; 
  autoSizeSettings[e9verb_autosize_maxheight]    = 150; 
  autoSizeSettings[e9verb_autosize_minwidth]     = 60;
  autoSizeSettings[e9verb_autosize_maxwidth]     = 200; 
  
  return autoSizeSettings;
}
static int[] CustomDialogGui::GetDefaultSettings_Background()
{
  int bgSettings[] = new int[e9verb_bg_enumsize];
  
  bgSettings[e9verb_bg_img]                = 0;
  bgSettings[e9verb_bg_img_scaling]        = 0;
  bgSettings[e9verb_bg_img_transparency]   = 0;
  bgSettings[e9verb_bg_color]              = 0;
  
  return bgSettings;
}


/***********************************************************************
 * PUBLIC FUNCTIONS
 * ApplySettings_
 * Sets the gui options passed in parameter
 *
 ***********************************************************************/

 
void CustomDialogGui::ApplySettings_Sprites(int settings[]) 
{
    for (int i=0; i< e9verb_sprites_enumsize; i++) { this.sprites[i] = settings[i]; }
}

void CustomDialogGui::ApplySettings_Text(int settings[])
{
    for (int i=0; i<e9verb_text_enumsize ; i++) { this.textSettings[i] = settings[i]; }
}


void CustomDialogGui::ApplySettings_Borders(int settings[])
{
    for (int i=0; i< e9verb_borders_enumsize; i++) { this.bordersSettings[i] = settings[i]; }
}


void CustomDialogGui::ApplySettings_Layout(int settings[])
{
    for (int i=0; i<e9verb_layout_enumsize ; i++) { this.layoutSettings[i] = settings[i]; }
}


void CustomDialogGui::ApplySettings_Autosize(int settings[])
{
    for (int i=0; i<e9verb_autosize_enumsize ; i++) { this.autoSizeSettings[i] = settings[i]; }
}


void CustomDialogGui::ApplySettings_Background(int settings[])
{
    for (int i=0; i<e9verb_bg_enumsize ; i++) { this.bgSettings[i] = settings[i]; }
}


  
  
  
/***********************************************************************
 * PUBLIC FUNCTION
 * init()
 * Set and modify your default GUI options here
 *
 ***********************************************************************/
function CustomDialogGui::init() {
  
  // Activate this to hide the action GUI while a dialog is active.
  this.hide_gui_while_dialog = true;  
  
  //Apply default layout settings
  int s[] = CustomDialogGui.GetDefaultSettings_Layout();
  this.ApplySettings_Layout(s);
  
  
  // xyscreeenborders define the closest distance to the screen.
  // This overrides gui_xpos and ypos.
  this.yscreenborder      = 0;
  this.xscreenborder      = 0;   

  // Text settings (font, etc.)
  s = CustomDialogGui.GetDefaultSettings_Text();
  this.ApplySettings_Text(s);
  this.text_numbering    = true; 
 
  // How many rows are to be scrolled per click
  this.scroll_rows = 1;
  
  // optional bullet image, 0 = off
  this.bullet = 0;
  
  // scrolling with mousewheel
  this.mousewheel      = true;
  
  // Always begins the dialog with the first option available
  this.reset_scrollstate = true;
  
  // First option on the bottom, last option on top
  this.dialog_options_upwards = false;
  
  s = CustomDialogGui.GetDefaultSettings_Sprites();
  this.ApplySettings_Sprites(s);

  
  // Amount of time, after scrolling is being processed
  // use this if you have push-images for scrolling arrows
  this.scroll_btn_delay = 0.5;
  
  // Autoalign arrows so you don't need to supply XY-coordinates 
  // highly recommended in combination with autoheight and autowidth
  // 0 = off, 1 = left, 2= center, 3=right
  this.auto_arrow_align = 3; 
  // Define the offset between the arrow sprite and the GUI edge. This value
  // is not affected by the borders, so you have to make sure that the offset
  // is not greater than the border size.
  this.auto_arrow_up_offset_x = 1;
  this.auto_arrow_up_offset_y = 1;
  this.auto_arrow_down_offset_x = 1;
  this.auto_arrow_down_offset_y = 1;  

  // borders - also needed for the scroll buttons
  s = CustomDialogGui.GetDefaultSettings_Borders();
  this.ApplySettings_Borders(s);
  this.border_visible  = false;
  
  // Background
  // set bg_img_transparency to -1 if you're using 32-bit graphics and
  // want to preserve the alpha channel
  s = CustomDialogGui.GetDefaultSettings_Background();
  this.ApplySettings_Background(s);
  
  // optional autosize, overrides your width and height setting
  // also overrides the position of your scrolling - arrows
  this.autosize_height                               = false; 
  this.autosize_width                                = false;
  s = CustomDialogGui.GetDefaultSettings_Autosize();
  this.ApplySettings_Autosize(s);
  // Options end 
}


/***********************************************************************
 * PRIVATE FUNCTION
 * prepare(DialogOptionsRenderingInfo)
 * Sets some global vars
 * 
 ***********************************************************************/
function _prepare(this CustomDialogGui*, DialogOptionsRenderingInfo *info)
{
  int i = 1;
  CDG.active_options_count = 1;
  in_speech = true;
  
  if (this.hide_gui_while_dialog == true && gMaingui.Visible == true && gAction.Visible == true) {
    gMaingui.Visible = false;
    gAction.Visible = false;
  }
  
  // count active options
  while (i <= info.DialogToRender.OptionCount) {
    if (info.DialogToRender.GetOptionState(i) == eOptionOn) CDG.active_options_count++;
    i++;
  }
  // prepare dynamic arrays
  CDG_active_options        = new int[CDG.active_options_count];
  CDG_active_options_height = new int[CDG.active_options_count];
  CDG_active_options_width  = new int[CDG.active_options_count];
  CDG_active_options_text   = new String[CDG.active_options_count];
}

/***********************************************************************
 * PRIVATE FUNCTION
 * _addOption(DialogOptionsRenderingInfo *info)
 * sets Dialog options up.
 * 
 ***********************************************************************/
function _addOption(this CustomDialogGui*, int position, int optionNumber, String optionText)
{
  String temp_option;
  int temp_text_height;
  CDG_active_options[position] = optionNumber;
  
  if (CDG.text_numbering) {
    if (CDG.dialog_options_upwards) temp_option = String.Format ("%d.",CDG.active_options_count-position);
    else temp_option = String.Format ("%d.",position);
    temp_option = temp_option.Append(" ");
    temp_option = temp_option.Append(optionText);     
  }
  else temp_option = optionText;
  
  CDG_active_options_text[position]  = temp_option;
  CDG_active_options_height[position]= GetTextHeight(temp_option, CDG.textSettings[e9verb_text_font], 
        CDG.layoutSettings[e9verb_gui_width] - CDG.bordersSettings[e9verb_border_left] - CDG.bordersSettings[e9verb_border_right])+CDG.textSettings[e9verb_text_line_space];
  CDG_active_options_width[position] = GetTextWidth(temp_option, CDG.textSettings[e9verb_text_font])+ CDG.bordersSettings[e9verb_border_left] + CDG.bordersSettings[e9verb_border_right] +2;
  if (CDG.bullet!=0) 
    CDG_active_options_width[position] += Game.SpriteWidth[CDG.bullet];
    
}



/***********************************************************************
 * PRIVATE FUNCTION
 * getOptionDetails(DialogOptionsRenderingInfo *info)
 * Get active option numbers, texts and their max height  
 * 
 ***********************************************************************/
function _getOptionDetails(this CustomDialogGui*,DialogOptionsRenderingInfo *info){
  int i = 1, j = 1, option_count;
  String temp_option, temp_string;
  
  option_count = info.DialogToRender.OptionCount;
  

  // Normal Sorting
  if (!CDG.dialog_options_upwards) {
    while (i <= option_count) {
      if (info.DialogToRender.GetOptionState(i) == eOptionOn) 
      {
        temp_string   = info.DialogToRender.GetOptionText(i);
        CDG._addOption(j, i, temp_string);
        j++;
      }
      i++;
    }       
  }
  // Bottom-Up sorting
  else {
    i = option_count;
    while (i >= 1) {
      if (info.DialogToRender.GetOptionState(i) == eOptionOn) 
      {
        temp_string   = info.DialogToRender.GetOptionText(i);
        CDG._addOption(j, i, temp_string);
        j++;
      }
      i--;
    }   
  }
  
  
}

/***********************************************************************
 * PRIVATE FUNCTION
 * _getRowCount()
 * Get the Number of Rows
 *
 * 
 ***********************************************************************/
function _getRowCount(this CustomDialogGui*, int width)
{ 
  int i, j, k = 1, temp_height; 
  bool first_call=false;
  
  j = 0;
  if ( CDG.scroll_from ==0) {
    CDG.scroll_from =1;      
    first_call = true;
  }
  i = CDG.scroll_from;
  

  temp_height =CDG.max_option_height;
  // How many options fit in the max_height?

  while (i < CDG.active_options_count)
  {        
    if (temp_height > CDG_active_options_height[i]) {
      temp_height -= CDG_active_options_height[i];
      j++;
    }
    else i = CDG.active_options_count-1;
    CDG.scroll_to = j;
    i++;
  }
  
  CDG.scroll_to += CDG.scroll_from-1; 
  if (CDG.scroll_to >= CDG.active_options_count) CDG.scroll_to = CDG.active_options_count-1;   
  
  // Reverse counting to scroll down to the last option
  if (CDG.dialog_options_upwards && first_call) { 
    i = CDG.active_options_count-1;
    j = 0;
    temp_height = CDG.max_option_height;
    while (i > 0) {
      if (temp_height > CDG_active_options_height[i]) {
        temp_height -= CDG_active_options_height[i];
        j++;
      }else i=0;        
      i--;
    }
    CDG.scroll_to = CDG.active_options_count-1;  
    
    if (j >= CDG.active_options_count-1) CDG.scroll_from = 1;
    else CDG.scroll_from = CDG.active_options_count -j;
  }
  
}


/***********************************************************************
 * PRIVATE FUNCTION
 * _repexec
 * formally dialog_options_get_active
 *
 * 
 ***********************************************************************/
function _repexec(this CustomDialogGui*, DialogOptionsRenderingInfo *info)
{
  int i=0, ypos = CDG.bordersSettings[e9verb_border_top], xpos = CDG.bordersSettings[e9verb_border_left], xpos_offset, j=1, 
  current_option;
  
  CDG_Arrow uparrow;
  CDG_Arrow downarrow;
    
  String temp_string, temp_option;
  bool linefeed_done;

  // Up-Arrow coordinates
  uparrow.x1 = info.X + CDG.layoutSettings[e9verb_uparrow_xpos];
  uparrow.y1 = info.Y + CDG.layoutSettings[e9verb_uparrow_ypos];
  uparrow.x2 = uparrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_uparrow_img]];
  uparrow.y2 = uparrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_uparrow_img]];

  // Down-Arrow coordinates
  downarrow.x1 = info.X + CDG.layoutSettings[e9verb_downarrow_xpos];
  downarrow.y1 = info.Y + CDG.layoutSettings[e9verb_downarrow_xpos];
  downarrow.x2 = downarrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_downarrow_img]];
  downarrow.y2 = downarrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_downarrow_img]];    
  
  // scroll up-down: highlight
  // Scroll up
  if ((mouse.x >= uparrow.x1 && mouse.y >= uparrow.y1)&&(mouse.x <= uparrow.x2 && mouse.y <= uparrow.y2)) 
  {

    if (CDG.uparrow_current_img !=CDG.sprites[e9verb_uparrow_hi_img] && CDG.sprites[e9verb_uparrow_hi_img]!=0) 
    { 
      CDG.uparrow_current_img = CDG.sprites[e9verb_uparrow_hi_img];
      CDG.downarrow_current_img = CDG.sprites[e9verb_downarrow_img];
#ifver 3.4
        info.Update();
#endif
      return;
    }
    
  }
  // Scroll down
  else if ((mouse.x >= downarrow.x1 && mouse.y >= downarrow.y1) && (mouse.x <= downarrow.x2 && mouse.y <= downarrow.y2)) 
  {

    if (CDG.downarrow_current_img !=CDG.sprites[e9verb_downarrow_hi_img] && CDG.sprites[e9verb_downarrow_hi_img] !=0) 
    {
      CDG.downarrow_current_img = CDG.sprites[e9verb_downarrow_hi_img];
      CDG.uparrow_current_img = CDG.sprites[e9verb_uparrow_img];
      #ifver 3.4
        info.Update();
      #endif      
      return;
    }
    
  }
  //Nothing
  else 
  {
    if ((CDG.downarrow_current_img !=CDG.sprites[e9verb_downarrow_img] ) ||(CDG.uparrow_current_img !=CDG.sprites[e9verb_uparrow_img])) 
    {
      CDG.uparrow_current_img = CDG.sprites[e9verb_uparrow_img];
      CDG.downarrow_current_img = CDG.sprites[e9verb_downarrow_img];     
      #ifver 3.4
        info.Update();
      #endif          
      return;
    }
  }

  i = CDG.scroll_from;
  // Active option for vertical alignment
  while (i <= CDG.scroll_to) {
    
    ypos += CDG_active_options_height[i];
    if ((mouse.y - info.Y) < ypos && 
        (mouse.y > info.Y + CDG.bordersSettings[e9verb_border_top]) &&
       ((mouse.x > info.X + CDG.bordersSettings[e9verb_border_left])) && 
        (mouse.x < info.X+ CDG.layoutSettings[e9verb_gui_width] - CDG.bordersSettings[e9verb_border_right]))
    {
      info.ActiveOptionID = CDG_active_options[i];    
      return;
    }
    else  if ((mouse.y - info.Y) < ypos || 
              (mouse.y - info.Y > info.Height - CDG.bordersSettings[e9verb_border_bottom]) ||
             ((mouse.x >info.X + CDG.layoutSettings[e9verb_gui_width] - CDG.bordersSettings[e9verb_border_right]))||
              (mouse.x<info.X))
     {        
      info.ActiveOptionID = 0;   
    }
    i++;
  }    
}

/***********************************************************************
 * AGS SUPPLIED FUNCTION 
 * dialog_options_get_dimensions
 * 
 ***********************************************************************/
function dialog_options_get_dimensions(DialogOptionsRenderingInfo *info)
{  
  int i=1, j=1, option_count=0, max_height=0, autoheight=0, active_options=1, 
      autowidth=0, max_width = 0,  temp_width = 0,  xpos,  ypos;
  String temp_option, temp_string;
    
    // Reserve space for bullet in textmode, if needed.
    if (CDG.bullet!=0) 
      if (CDG.bordersSettings[e9verb_border_left] <Game.SpriteWidth[CDG.bullet]) {
        CDG.bordersSettings[e9verb_border_left]   += Game.SpriteWidth[CDG.bullet];
    }

  ////////////////////////////////////////////////////////
  // calculate autowidth and autoheight                 //
  ////////////////////////////////////////////////////////
  if (CDG.autosize_height)
    CDG.max_option_height = CDG.autoSizeSettings[e9verb_autosize_maxheight] - CDG.bordersSettings[e9verb_border_bottom] - CDG.bordersSettings[e9verb_border_top];
  else
    CDG.max_option_height = CDG.layoutSettings[e9verb_gui_height] - CDG.bordersSettings[e9verb_border_bottom] - CDG.bordersSettings[e9verb_border_top];  
    
  if (CDG.autosize_height || CDG.autosize_width) {
    option_count = info.DialogToRender.OptionCount;       
    
    CDG._prepare(info);
    CDG._getOptionDetails(info);
    

    while (i <= CDG.active_options_count-1) {
      max_height += CDG_active_options_height[i];
      temp_width = CDG_active_options_width[i];
      if (max_width < temp_width) max_width = temp_width;
      i++;
    }
    i=1;
    
    // check for min and max sizes
    autoheight = max_height + CDG.bordersSettings[e9verb_border_top] + CDG.bordersSettings[e9verb_border_bottom] +2;
    
    
    if (autoheight > CDG.autoSizeSettings[e9verb_autosize_maxheight]) autoheight = CDG.autoSizeSettings[e9verb_autosize_maxheight];
    else if (autoheight <= CDG.autoSizeSettings[e9verb_autosize_minheight]) autoheight = CDG.autoSizeSettings[e9verb_autosize_minheight]; 
    
    autowidth = max_width + CDG.bordersSettings[e9verb_border_left]+CDG.bordersSettings[e9verb_border_right] +2;
    if (autowidth > CDG.autoSizeSettings[e9verb_autosize_maxwidth]) autowidth = CDG.autoSizeSettings[e9verb_autosize_maxwidth];
    else if (autowidth <= CDG.autoSizeSettings[e9verb_autosize_minwidth]) autowidth = CDG.autoSizeSettings[e9verb_autosize_minwidth];
  }
  
  if (!CDG.autosize_height) autoheight = CDG.layoutSettings[e9verb_gui_height];
  if (!CDG.autosize_width) autowidth = CDG.layoutSettings[e9verb_gui_width];  
  
  xpos = CDG.layoutSettings[e9verb_gui_xpos];
  ypos = CDG.layoutSettings[e9verb_gui_ypos];      

// Check on Screenborders   
  
  if ((ypos + autoheight + CDG.yscreenborder) > System.ViewportHeight) {
    ypos = System.ViewportHeight - autoheight - CDG.yscreenborder;  
  }
  else if (ypos < CDG.yscreenborder) ypos =  CDG.yscreenborder;
    
  if ((xpos + autowidth + CDG.xscreenborder) > System.ViewportWidth) {
    xpos = System.ViewportWidth - autowidth - CDG.xscreenborder;      
  }
  else if (xpos < CDG.xscreenborder) xpos = CDG.xscreenborder;
      
  ////////////////////////////////////////////////////////
  // Set GUI sizes                                      //
  ////////////////////////////////////////////////////////      
  
  if (CDG.autosize_height) {  
    CDG.layoutSettings[e9verb_gui_height] = autoheight;    
    
    if ((autoheight + ypos + CDG.yscreenborder) > System.ViewportHeight) {
      //CDG.bgSettings[e9verb_bg_color]=1;
      CDG.layoutSettings[e9verb_gui_height] = System.ViewportHeight - ypos - CDG.yscreenborder;
    }
              
    else if ((CDG.layoutSettings[e9verb_gui_height] + ypos + CDG.yscreenborder) > System.ViewportHeight) {
      //CDG.bgSettings[e9verb_bg_color]=1;
      CDG.layoutSettings[e9verb_gui_height] = System.ViewportHeight - ypos - CDG.yscreenborder;
    }     
  }      

  if (CDG.autosize_width) {
    CDG.layoutSettings[e9verb_gui_width] = autowidth;
    if (autowidth + xpos + CDG.xscreenborder > System.ViewportWidth) {
      CDG.layoutSettings[e9verb_gui_width] = System.ViewportWidth- xpos - CDG.xscreenborder;  
    }        
    else if (CDG.layoutSettings[e9verb_gui_width] + xpos + CDG.xscreenborder > System.ViewportWidth) {  
      CDG.layoutSettings[e9verb_gui_width] = System.ViewportWidth- xpos - CDG.xscreenborder;  
    } 
  }  
    
  ////////////////////////////////////////////////////////
  // Arrow alignment                                    //
  ////////////////////////////////////////////////////////
  if (CDG.auto_arrow_align >0) {
   CDG.layoutSettings[e9verb_uparrow_ypos]   = CDG.auto_arrow_up_offset_y;
   CDG.layoutSettings[e9verb_downarrow_ypos] = CDG.layoutSettings[e9verb_gui_height] - Game.SpriteHeight[CDG.sprites[e9verb_downarrow_img]] - CDG.auto_arrow_down_offset_y;  

      if (CDG.auto_arrow_align == 1) {
        CDG.layoutSettings[e9verb_uparrow_xpos]   = CDG.bordersSettings[e9verb_border_left]+ CDG.auto_arrow_up_offset_x;
        CDG.layoutSettings[e9verb_downarrow_xpos] = CDG.bordersSettings[e9verb_border_left]+ CDG.auto_arrow_down_offset_x;
      }
      else if (CDG.auto_arrow_align == 2) {
        CDG.layoutSettings[e9verb_uparrow_xpos]   = CDG.layoutSettings[e9verb_gui_width] / 2 - Game.SpriteWidth[CDG.sprites[e9verb_downarrow_img]] / 2;
        CDG.layoutSettings[e9verb_downarrow_xpos] = CDG.layoutSettings[e9verb_uparrow_xpos];
      }      
      else if (CDG.auto_arrow_align ==3) {
        CDG.layoutSettings[e9verb_uparrow_xpos]   = CDG.layoutSettings[e9verb_gui_width] - Game.SpriteWidth[CDG.sprites[e9verb_uparrow_img]] - CDG.auto_arrow_up_offset_x; 
        CDG.layoutSettings[e9verb_downarrow_xpos] = CDG.layoutSettings[e9verb_gui_width] - Game.SpriteWidth[CDG.sprites[e9verb_downarrow_img]] - CDG.auto_arrow_down_offset_x; 
      }   
  }

  
  //Position of GUI
  info.X = xpos ;
  info.Y = ypos ;  
  //Size of GUI
  info.Width  = CDG.layoutSettings[e9verb_gui_width] ;
  info.Height = CDG.layoutSettings[e9verb_gui_height];  
  
  //if (CDG.scroll_from == 0)CDG.scroll_from = 1;
}



/***********************************************************************
 * AGS SUPPLIED FUNCTION 
 * dialog_options_render
 * 
 ***********************************************************************/
function dialog_options_render(DialogOptionsRenderingInfo *info)
{

  int i = 1, j = 1, k = 1, ypos = CDG.bordersSettings[e9verb_border_top], ypos_offset, xpos = CDG.bordersSettings[e9verb_border_left], xpos_offset, current_height, 
      option_count=0, current_option, temp_height, temp_text_height;
  String temp_option;
  bool linefeed_done;
   
  option_count = info.DialogToRender.OptionCount;
  
  ////////////////////////////////////////////////////////
  // Draw GUI decorations                               //
  ////////////////////////////////////////////////////////
  CDG.dialog_window = info;
 
  // Fill GUI Background
  if (CDG.bgSettings[e9verb_bg_img]==0) info.Surface.Clear(CDG.bgSettings[e9verb_bg_color]);
  else 
  {
   if (CDG.bgSettings[e9verb_bg_img_scaling]==1) {
      if (CDG.bgSettings[e9verb_bg_img_transparency] == -1) info.Surface.DrawImage(0, 0, CDG.bgSettings[e9verb_bg_img], 0, info.Width, info.Height);
      else info.Surface.DrawImage(0, 0, CDG.bgSettings[e9verb_bg_img], CDG.bgSettings[e9verb_bg_img_transparency], info.Width, info.Height);        
    }
    else {
      if (CDG.bgSettings[e9verb_bg_img_transparency] == -1) info.Surface.DrawImage(0, 0, CDG.bgSettings[e9verb_bg_img]);
      else info.Surface.DrawImage(0, 0, CDG.bgSettings[e9verb_bg_img], CDG.bgSettings[e9verb_bg_img_transparency]);
    }
  }
 
  // Outline
  if (CDG.border_visible) {
    info.Surface.DrawingColor = CDG.bordersSettings[e9verb_border_color];
    info.Surface.DrawLine(0, 0, info.Width, 0);
    info.Surface.DrawLine(0, 0, 0, info.Height);
    if (System.ViewportWidth>320) {
      info.Surface.DrawLine(0, info.Height, info.Width, info.Height);
      info.Surface.DrawLine(info.Width, 0, info.Width, info.Height);   
    }
    else {
      info.Surface.DrawLine(0, info.Height-1, info.Width, info.Height-1);
      info.Surface.DrawLine(info.Width-1, 0, info.Width-1, info.Height);  
    }
  }
  CDG._prepare(info);
  CDG._getOptionDetails(info);

  ////////////////////////////////////////////////////////
  // Calculate, how many options fit in the GUI         //
  ////////////////////////////////////////////////////////
  CDG._getRowCount(CDG.layoutSettings[e9verb_gui_width]);
  
  ////////////////////////////////////////////////////////
  // Finally draw the options                           //
  ////////////////////////////////////////////////////////
  i = CDG.scroll_from;
  

  while (i <= CDG.scroll_to)
  { 
    current_option = CDG_active_options[i];
    
    if (info.DialogToRender.GetOptionState(current_option) == eOptionOn)
    {             
      if (info.ActiveOptionID == current_option) info.Surface.DrawingColor = CDG.textSettings[e9verb_text_color_active];
      else info.Surface.DrawingColor = CDG.textSettings[e9verb_text_color];
      
      if (CDG.textSettings[e9verb_text_bg]!=0) {
          if (CDG.textSettings[e9verb_text_bg_scaling]==1)
            info.Surface.DrawImage(CDG.textSettings[e9verb_text_bg_xpos], ypos, CDG.textSettings[e9verb_text_bg], CDG.textSettings[e9verb_text_bg_transparency], 
            CDG.layoutSettings[e9verb_gui_width] - CDG.textSettings[e9verb_text_bg_xpos] - CDG.bordersSettings[e9verb_border_left] , CDG_active_options_height[i]);
          else info.Surface.DrawImage(CDG.textSettings[e9verb_text_bg_xpos], ypos, CDG.textSettings[e9verb_text_bg], CDG.textSettings[e9verb_text_bg_transparency]);
      }
      
      if (CDG.bullet!=0) info.Surface.DrawImage (CDG.bordersSettings[e9verb_border_left] - Game.SpriteWidth[CDG.bullet], ypos, CDG.bullet);

      info.Surface.DrawStringWrapped(CDG.bordersSettings[e9verb_border_left], ypos, CDG.layoutSettings[e9verb_gui_width] - CDG.bordersSettings[e9verb_border_left]-CDG.bordersSettings[e9verb_border_right], 
                         CDG.textSettings[e9verb_text_font], CDG.textSettings[e9verb_text_alignment], CDG_active_options_text[i]);
      
      ypos += CDG_active_options_height[i];
    } 
    
    i++;
  }


 
  // Remove pushed state, if the mouse has left the buttons
  CDG_Arrow uparrow;
  CDG_Arrow downarrow;

  // Up-Arrow coordinates
  uparrow.x1 = info.X + CDG.layoutSettings[e9verb_uparrow_xpos];
  uparrow.y1 = info.Y + CDG.layoutSettings[e9verb_uparrow_ypos] ;
  uparrow.x2 = uparrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_uparrow_img]];
  uparrow.y2 = uparrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_uparrow_img]];

  // Down-Arrow coordinates
  downarrow.x1 = info.X + CDG.layoutSettings[e9verb_downarrow_xpos];
  downarrow.y1 = info.Y + CDG.layoutSettings[e9verb_downarrow_ypos] ;
  downarrow.x2 = downarrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_downarrow_img]];
  downarrow.y2 = downarrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_downarrow_img]];   
  
  if (!((mouse.x >= uparrow.x1 && mouse.y >= uparrow.y1)&&(mouse.x <= uparrow.x2 && mouse.y <= uparrow.y2)) &&
      !((mouse.x >= downarrow.x1 && mouse.y >= downarrow.y1) && (mouse.x <= downarrow.x2 && mouse.y <= downarrow.y2))){
        CDG.uparrow_current_img = CDG.sprites[e9verb_uparrow_img];
        CDG.downarrow_current_img = CDG.sprites[e9verb_downarrow_img];
      }
   
  
  // Draw scrolling sprites
  if (CDG.scroll_from!=1 ){ 
    info.Surface.DrawImage(CDG.layoutSettings[e9verb_uparrow_xpos], CDG.layoutSettings[e9verb_uparrow_ypos],  CDG.uparrow_current_img);
  }

  if (CDG.scroll_to != CDG.active_options_count-1 ) 
    info.Surface.DrawImage(CDG.layoutSettings[e9verb_downarrow_xpos], CDG.layoutSettings[e9verb_downarrow_ypos],  CDG.downarrow_current_img);    
}


/***********************************************************************
 * AGS SUPPLIED FUNCTION in 3.2 and 3.3
 * dialog_options_get_active
 * Highlight the textoptions on mouseover
 *
 ***********************************************************************/
#ifnver 3.4
function dialog_options_get_active(DialogOptionsRenderingInfo *info)
{
  CDG._repexec(info);
}
#endif
/***********************************************************************
 * AGS SUPPLIED FUNCTION in 3.4
 * dialog_options_repexec
 * repexec for custom dialog rendering
 *
 ***********************************************************************/
#ifver 3.4
function dialog_options_repexec(DialogOptionsRenderingInfo *info)
{
  CDG._repexec(info);
}
#endif

/***********************************************************************
 * AGS SUPPLIED FUNCTION
 * dialog_options_mouse_click
 * 
 ***********************************************************************/
function dialog_options_mouse_click(DialogOptionsRenderingInfo *info, MouseButton button)
{

  CDG_Arrow uparrow;
  CDG_Arrow downarrow;
  int i;

  // Up-Arrow coordinates
  uparrow.x1 = info.X + CDG.layoutSettings[e9verb_uparrow_xpos];
  uparrow.y1 = info.Y + CDG.layoutSettings[e9verb_uparrow_ypos] ;
  uparrow.x2 = uparrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_uparrow_img]];
  uparrow.y2 = uparrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_uparrow_img]];

  // Down-Arrow coordinates
  downarrow.x1 = info.X + CDG.layoutSettings[e9verb_downarrow_xpos];
  downarrow.y1 = info.Y + CDG.layoutSettings[e9verb_downarrow_ypos] ;
  downarrow.x2 = downarrow.x1 + Game.SpriteWidth[CDG.sprites[e9verb_downarrow_img]];
  downarrow.y2 = downarrow.y1 + Game.SpriteHeight[CDG.sprites[e9verb_downarrow_img]];
  

  // scroll up
  if (((mouse.x >= uparrow.x1 && mouse.y >= uparrow.y1) &&
       (mouse.x <= uparrow.x2 && mouse.y <= uparrow.y2))||
       (button == eMouseWheelNorth && CDG.mousewheel)) {
        i=0;
        
        while (i<CDG.scroll_rows)
        {
          if (CDG.scroll_from >1) CDG.scroll_from --;
          dialog_options_render(info);          
          i++;
        }
  } 
  // scroll down
  else if (((mouse.x >= downarrow.x1 && mouse.y >= downarrow.y1) &&
            (mouse.x <= downarrow.x2 && mouse.y <= downarrow.y2)) ||
            (button == eMouseWheelSouth && CDG.mousewheel)) {
      
      i=0; 
      while (i<CDG.scroll_rows)
      {      
        if (CDG.scroll_to != CDG.active_options_count-1) {
          dialog_options_render(info); 
          CDG.scroll_from ++;
        }
        i++;
      }
  }
  #ifnver 3.4
  dialog_options_get_active(info);  
  #endif
  #ifver 3.4
  info.Update();
  if (button != eMouseWheelSouth && button != eMouseWheelNorth) info.RunActiveOption();
  #endif
}

function game_start() 
{
   CDG.uparrow_current_img = CDG.sprites[e9verb_uparrow_img];
   CDG.downarrow_current_img = CDG.sprites[e9verb_downarrow_img];   
   CDG.init();
   
   CDG.sprites[0] = 100;
}



function repeatedly_execute() 
{
  if (CDG.reset_scrollstate) { 
    if (CDG.dialog_options_upwards) CDG.scroll_from =0;
    else CDG.scroll_from = 1;
  }
  if (in_speech == true) {
    in_speech = false;
    if (CDG.hide_gui_while_dialog == true && gMaingui.Visible == false && gAction.Visible == false) {
      gMaingui.Visible = true;
      gAction.Visible = true;
    }
  }
}

export CDG;