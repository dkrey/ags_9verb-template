// main global script file

// Globals from GUIscript
import int ActionLabelColorHighlighted;
import char key_l_yes, key_u_yes, key_l_no, key_u_no; 
import int action_l_keycode[A_COUNT_]; 
import int action_u_keycode[A_COUNT_];
import int GStopsaveitem;
import int listBoxGap;
import int lang;
import InventoryItem*ItemGiven; 

// Door IDs in use are:
///////////////////////////////////////////////
// ID 20 - Sample Door in Room 1
///////////////////////////////////////////////

// =======================================================================================
function game_start() {

  String tr_lang;
  // --- translate GUI action buttons ---
  tr_lang = GetTranslation("GUI_LANGUAGE");
  tr_lang = tr_lang.LowerCase();
  
  if (tr_lang == "de") {
    lang = eLangDE;
  }
  else if (tr_lang =="es") {
    lang = eLangES;
  }
  else if (tr_lang =="fr") {
    lang = eLangFR;
  }  
  else if (tr_lang =="en") {
    lang = eLangEN;
  }
  else if (tr_lang =="it") {
    lang = eLangIT;
  }     
  InitGuiLanguage();
  AdjustGUIText();
  // --- Set default Door strings (Look, locked and wrong item)
  set_door_strings("The door looks solid.","It is locked.","I can't unlock it with that.","I have to close it first.","Now it's unlocked.","The door is locked again.");
  // --- initialize game settings ---
  SetDefaultAction(eMA_WalkTo);    
  set_double_click_speed(GetGameSpeed()/4);
  
  // --- set the pixel gap for listbox items according to the screen res
  if (System.ScreenWidth<640) listBoxGap = 2; 
  else listBoxGap = 4;
}

function on_mouse_click(MouseButton button) {
}

function repeatedly_execute() {
}

function on_key_press(eKeyCode keycode) {
  // called when a key is pressed. keycode holds the key's ASCII code
  
  // --- PAUSE-GUI ---
  if (keycode == eKeySpace)
  {
    // SPACEBAR
    if (!IsGamePaused()) {
      PauseGame();
      AdjustGUIText();
      gPaused.Visible=true;
    }
    else {
      gPaused.Visible=false;
      UnPauseGame();
      //SetDefaultAction(eMA_WalkTo);    
      //ActionLine.TextColor=ActionLabelColorHighlighted;      
      CheckDefaultAction();
      UpdateActionBar();
    }
  }
  // --- RESTART-GUI ---
  else if (gRestart.Visible) {

    // if exit gui is on
    if (keycode==key_u_no || keycode == key_l_no) gRestart.Visible=false; // if N is pressed continue
    if (keycode==key_u_yes || keycode == key_l_yes) RestartGame(); // if Y is pressed restart game
  }
  // --- QUITGAME-GUI ---
  if (gConfirmexit.Visible) {
    // if exit gui is on
    if (keycode==key_u_no || keycode==key_l_no) gConfirmexit.Visible=false; // if N is pressed continue
    if (keycode==key_u_yes || keycode==key_l_yes) QuitGame(0);       // if Y is pressed quit game
  }
  
  // Translate GUIs on keypress
  if (keycode == eKeyF5 || keycode == eKeyF8 || keycode == eKeyCtrlC) AdjustGUIText();
  
  // 
  if (keycode == eKeyF5) gOptions.Visible=true;            // F5 - OPTIONS
  if (keycode == eKeyF8) gRestart.Visible=true;            // F8 - RESTART
  if (keycode == eKeyCtrlC) gConfirmexit.Visible=true;          // Ctrl-C - QUIT
  if (keycode == eKeyF12)   SaveScreenShot("scrnshot.pcx");  // F12
  if (keycode == eKeyCtrlS) Debug (0, 0);                     // Ctrl-S, give all inventory
  if (keycode == eKeyCtrlV) Debug (1, 0);                     // Ctrl-V, version
  if (keycode == eKeyCtrlA) Debug (2, 0);                     // Ctrl-A, show walkable areas
  if (keycode == eKeyCtrlX) Debug (3, 0);                     // Ctrl-X, teleport to room
  
  // --- triggering actions by the keys:---
  
  if (!IsGamePaused()) {
    int act_i=0;
    while (act_i<A_COUNT_) {
      if (keycode == action_l_keycode[act_i] || keycode==action_u_keycode[act_i]) {
        SetAction(act_i);
        act_i=A_COUNT_;
      }
      else {
        act_i++;
      }
    }
  }
}

function on_event(EventType event, int data) {
  if (event==eEventLeaveRoom)
  if (event==eEventRestoreGame) {
    AdjustLanguage();
  }
  if (event==eEventEnterRoomBeforeFadein || event==eEventRestoreGame)
    player.PlaceOnWalkableArea();
}


////////////////////////////////////////////////////////////////////////////////////
// GUI handling
////////////////////////////////////////////////////////////////////////////////////

function Action_Click(GUIControl *control, MouseButton button) {
  Action tempbutton = getButtonAction(control.ID);
  SetAction(tempbutton);
}

function btnMainOpt_OnClick(GUIControl *control, MouseButton button)
{
  AdjustGUIText();
  if (!gRestart.Visible && !gConfirmexit.Visible)
  gOptions.Visible=true;
}

function ScrollInv_Click(GUIControl *control, MouseButton button) {
  if (control==InvUp) MainInv.ScrollUp();
  else MainInv.ScrollDown();  
}

function Options_Click(GUIControl *control, MouseButton button) {
	if (control==OptionsSave)	{
		//save game
		gOptions.Visible=false;
		GetLucasSavegameListBox(SaveListBox);
		gSave.Visible=true;
	}
	if (control==OptionsLoad)	{
		//load game
		gOptions.Visible=false;
		GetLucasSavegameListBox(RestoreListBox);
    RestoreListBox.TopItem=0;
		gRestore.Visible=true;
	}
	if (control==OptionsPlay)	{
		// continue playing
		gOptions.Visible=false;
	}
	if (control==OptionsQuit)	{
		// exit??
    gOptions.Visible=false;
		gConfirmexit.Visible=true;
	}
}

function OptionsRestart_OnClick(GUIControl *control, MouseButton button)
{
    gOptions.Visible=false;
		gRestart.Visible=true;
}

function OptionsDefault_OnClick(GUIControl *control, MouseButton button)
{
  OptionsSldMusic.Value = 80;
  OptionsSldSound.Value = 90;
  OptionsSldSpeed.Value = 40;
#ifdef USE_OBJECT_ORIENTED_AUDIO
  Game.SetAudioTypeVolume(eAudioTypeMusic, OptionsSldMusic.Value, eVolExistingAndFuture);  
  Game.SetAudioTypeVolume(eAudioTypeSound, OptionsSldSound.Value, eVolExistingAndFuture); 
  Game.SetAudioTypeVolume(eAudioTypeAmbientSound, OptionsSldSound.Value, eVolExistingAndFuture); 
#endif

#ifndef USE_OBJECT_ORIENTED_AUDIO
  SetSoundVolume(OptionsSldSound.Value);
  SetMusicVolume(OptionsSldMusic.Value);
#endif
  SetGameSpeed(OptionsSldSpeed.Value);
}

function OptionsSldMusic_OnChange(GUIControl *control)
{
#ifdef USE_OBJECT_ORIENTED_AUDIO
  Game.SetAudioTypeVolume(eAudioTypeMusic, OptionsSldMusic.Value, eVolExistingAndFuture);
#endif
#ifndef USE_OBJECT_ORIENTED_AUDIO
  SetMusicVolume(OptionsSldMusic.Value);
#endif

}
function OptionsSldSound_OnChange(GUIControl *control)
{
#ifdef USE_OBJECT_ORIENTED_AUDIO
  Game.SetAudioTypeVolume(eAudioTypeSound, OptionsSldSound.Value, eVolExistingAndFuture); 
  Game.SetAudioTypeVolume(eAudioTypeAmbientSound, OptionsSldSound.Value, eVolExistingAndFuture); 
#endif
#ifndef USE_OBJECT_ORIENTED_AUDIO
  SetSoundVolume(OptionsSldSound.Value);
#endif  

}


function OptionsSldSpeed_OnChange(GUIControl *control)
{
  SetGameSpeed(OptionsSldSpeed.Value);
}

function Restore_Click(GUIControl *control, MouseButton button) {
  if (control==RestoreCancel) gRestore.Visible=false;
  if (control==RestoreScrollUp)	{
  	if (GStopsaveitem < 5) GStopsaveitem = 0;
  	else GStopsaveitem -= 5;
  	RestoreListBox.TopItem=GStopsaveitem;
  }
  if (control==RestoreScrollDown) {
  if (GStopsaveitem < 90) {
      GStopsaveitem += 5;
      RestoreListBox.TopItem=GStopsaveitem;
    }
  }
}

function RestoreListBox_Click(GUIControl *control) {
  int index = RestoreListBox.SelectedIndex;
  String buffer=Game.GetSaveSlotDescription(index+100);
  if (buffer!=null) {
    gRestore.Visible=false;
    RestoreGameSlot(index + 100);
  } 
}

function Save_Click(GUIControl *control, MouseButton button) {
	int index = SaveListBox.SelectedIndex;
	String buffer;
	if (control==SaveCancel) {
		gSave.Visible=false;
		gSavetextbox.Visible=false;
	}
	if (control==SaveOK && index >= 0) {
		buffer=SaveTextBox.Text;
		gSave.Visible=false;
		gSavetextbox.Visible=false;
		SaveGameSlot (index + 100, buffer);
	}
	if (control==SaveScrollUp) {
		gSavetextbox.Visible=false;
		SaveListBox.SelectedIndex=-1;
		if (GStopsaveitem < 5) GStopsaveitem = 0;
		else GStopsaveitem -= 5;
		SaveListBox.TopItem=GStopsaveitem;
	}
	if (control==SaveScrollDown && GStopsaveitem < 90) {
		gSavetextbox.Visible=false;
		SaveListBox.SelectedIndex=-1;
		GStopsaveitem += 5;
		SaveListBox.TopItem=GStopsaveitem;
	}
}

function SaveListBox_Click(GUIControl *control) {
  int saveBox_ypos;
  int saveBox_xpos;
 	int index = SaveListBox.SelectedIndex;

	String buffer=String.Format("%d.", index+1);
	SaveLabel.Text=buffer;
	buffer=Game.GetSaveSlotDescription(index+100);
	if (buffer==null) buffer="";
	SaveTextBox.Text=buffer;
	
  saveBox_ypos = gSave.Y + SaveListBox.Y + ((index - GStopsaveitem) * (GetTextHeight(SaveLabel.Text, SaveLabel.Font, SaveLabel.Width)+listBoxGap));
  saveBox_xpos = GetTextWidth(SaveLabel.Text, SaveLabel.Font);
  SaveTextBox.SetPosition(saveBox_xpos, 0);
  
  gSavetextbox.SetPosition(gSave.X + SaveListBox.X, saveBox_ypos);
	gSavetextbox.Visible=true;
}

function SaveTextBox_Click(GUI *theGui, MouseButton button) {
  if (mouse.IsButtonDown(eMouseRight)) gSavetextbox.Visible=false;  
}


function dialog_request(int param) {
}

//////////////////////////////////////////////////////////////////////////
///////////////////Custom FUNCTIONS//////////////////////////////////
//////////////////////////////////////////////////////////////////////////

/* Character, Object, Hotspot full blown SAMPLE
function cChar_AnyClick() {

  // TALK TO (characters only)
  if (UsedAction(eGA_TalkTo)) {
    Unhandled();
  }
  // LOOK AT
  else if(UsedAction(eGA_LookAt)) {
    Unhandled();
  }
  // OPEN
  else if(UsedAction(eGA_Open)) {
    Unhandled();
  }  
  // CLOSE
  else if(UsedAction(eGA_Close)) {
    Unhandled();
  }
  // USE
  else if(UsedAction(eGA_Use)) {
    Unhandled();
  }
  // Push
  else if(UsedAction(eGA_Push)) {
    Unhandled();
  }
  // Pull
  else if(UsedAction(eGA_Pull)) {
    Unhandled();
  } 
  // PICKUP
  else if(UsedAction(eGA_PickUp)) {
    Unhandled();
  }
  // GIVE TO (characters only)
  else if(UsedAction(eGA_GiveTo)) {
    Unhandled();
  }  
  //USE INV
  else if(UsedAction(eGA_UseInv)) {
    Unhandled();
  }
  else Unhandled();
}
*/

/* Inventory SAMPLE
  // LOOK AT
  else if(UsedAction(eGA_LookAt)) {
    Unhandled();
  }
  // USE
  else if(UsedAction(eGA_Use)) {
    Unhandled();
  }
  // Push
  else if(UsedAction(eGA_Push)) {
    Unhandled();
  }
  // Pull
  else if(UsedAction(eGA_Pull)) {
    Unhandled();
  }   
  //USE INV
  else if(UsedAction(eGA_UseInv)) {
    Unhandled();
  }
  else Unhandled();

*/

function cBman_AnyClick()
{ 
  // TALK TO
  if (UsedAction(eGA_TalkTo)) {
    dDialog1.Start();
  }
  // LOOK AT
  else if(UsedAction(eGA_LookAt)) {
    Unhandled();
  }
  // OPEN
  else if(UsedAction(eGA_Open)) {
    Unhandled();
  }  
  // CLOSE
  else if(UsedAction(eGA_Close)) {
    Unhandled();
  }
  // USE
  else if(UsedAction(eGA_Use)) {
   Unhandled();
  }
  // Push
  else if(UsedAction(eGA_Push)) {
    Unhandled();
  }
  // Pull
  else if(UsedAction(eGA_Pull)) {
    Unhandled();
  } 
  // PICKUP
  else if(UsedAction(eGA_PickUp)) {
    Unhandled();
  }
  // GIVE TO
  else if (UsedAction(eGA_GiveTo)) {
    if (ItemGiven == iCup) {
      player.Say("Do you want this Cup?");
      cBman.Say("No, thank you.");
    }
    else Unhandled();
  }
  //USE INV
  else if(UsedAction(eGA_UseInv)) {
    player.Say("I'd rather give it to him.");
  }
  else Unhandled();
}


function iCup_OtherClick()
{
  // LOOK AT
  if(UsedAction(eGA_LookAt)) {
    player.Say("It's a blue cup.");
  }
  // USE
  else if(UsedAction(eGA_Use)) {
    Unhandled();
  }
  // Push
  else if(UsedAction(eGA_Push)) {
    Unhandled();
  }
  // Pull
  else if(UsedAction(eGA_Pull)) {
    Unhandled();
  } 
  //USE INV
  else if(UsedAction(eGA_UseInv)) {
    Unhandled();
  }
  else Unhandled();
}

function iEnvClose_OtherClick()
{
  if(UsedAction(eGA_Use)) {
    player.Say("Let's have a look...");
    Wait(5);
    player.LoseInventory(iEnvClose);
    player.AddInventory(iEnvOpen);
    Wait(5);
    player.Say("There's indeed a key inside this envelope.");
    player.AddInventory(iKey);
  }
  else if (UsedAction(eGA_LookAt))
  {
    player.Say("Beman gave me this.");
    player.Say("It seems like it's not empty.");
  }
  else Unhandled();
}

function iEnvOpen_OtherClick()
{
  if (UsedAction(eGA_LookAt))
  {
    player.Say("An empty envelope.");
  }
  else if(UsedAction(eGA_UseInv)) {
    if (player.ActiveInventory == iKey) {   
      player.Say("I'll better put it back again.");
      Wait(5);
      player.LoseInventory(iKey);
      Wait(5);
      player.LoseInventory(iEnvOpen);
      player.AddInventory(iEnvClose);     
    }
    else Unhandled();  
  }
  else Unhandled();  
}

function iKey_OtherClick()
{
  if(UsedAction(eGA_LookAt)) {
    player.Say("It's a small key.");
  }
  else Unhandled();
}
